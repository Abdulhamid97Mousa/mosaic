plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'org.jason.grpc'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':jason-interpreter')
    implementation 'io.grpc:grpc-netty-shaded:1.67.1'
    implementation 'io.grpc:grpc-protobuf:1.67.1'
    implementation 'io.grpc:grpc-stub:1.67.1'
    implementation 'com.google.protobuf:protobuf-java:4.27.2'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

application {
    mainClass = 'org.jason.grpc.bridge.RunBridgeMas'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.27.2'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.67.1'
        }
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir "${rootProject.projectDir}/../proto"
        }
    }
}

tasks.named('run') {
    args = ['src/main/resources/grpc_bridge.mas2j']
}

tasks.test {
    useJUnitPlatform()
}
